apply plugin: 'java'

version = '4.4.2'
group = 'ch.neiva'

// zxcvbn.js

def zxcvbnJs = file('src/main/resources/zxcvbn.js')

task downloadZxcvbnJs {
    inputs.property 'version', version
    outputs.file zxcvbnJs

    def zxcvbnJsUrl = "https://raw.githubusercontent.com/dropbox/zxcvbn/v${version}/dist/zxcvbn.js"
    doLast {
        zxcvbnJs << zxcvbnJsUrl.toURL().text
    }
}

task checksumZxcvbnJs {
    dependsOn downloadZxcvbnJs
    doLast {
        ant.checksum file: zxcvbnJs, algorithm: 'SHA-512'
    }
}

processResources.dependsOn downloadZxcvbnJs, checksumZxcvbnJs

// Benchmark

apply plugin: 'application'
mainClassName = 'ZxcvbnBench'

// Publish

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url 'repo'
        }
    }
}
